module Main (module Main) where

import Control.Monad (forM_)
import Data.List

import Language.LIMA
import Language.LIMA.Graph
import Language.LIMA.C
import Language.Sally

import Periodic

-- | Atoms to compile
as :: [Atom ()]
as = [ex1, ex2, ex3, ex4, ex5]

main :: IO ()
main = do
  let nm i = "ex" ++ show i
  forM_ (zip [1..] as) $ \(i,a) -> do
    compileToC (nm i) a
    let prefix = "periodic_" ++ nm i
    compileToSally prefix defaultCfg (prefix ++ ".mcmt") a Nothing
    graphAtom prefix a


-- C Code Generator Utilities --------------------------------------------

-- | Invoke the atom compiler, generating 'om1.{c,h}'
-- Also print out info on the generated schedule.
compileToC :: Name -> Atom () -> IO ()
compileToC nm atm = do
  res <- compile nm cfg atm
  putStrLn $ reportSchedule (compSchedule res)
  where
    cfg = defaults { cCode = prePostCode nm
                   , hCode = prePostH
                   }

-- | Custom pre-post code for generated C
prePostCode :: Name -> [Name] -> [Name] -> [(Name, Type)] -> (String, String)
prePostCode nm assertNames _covNames _probeNames =
  ( unlines [ "#include <stdio.h>"
            , "#include <unistd.h>"
            , ""
            , "static char* assert_names[" ++ show (length assertNames) ++ "] = "
              ++ "{" ++ intercalate "," (map (\n -> "\"" ++ n ++ "\"")  assertNames) ++ "};"
            , ""
            , "void assert(int id, bool c, int64_t clk) {"
            , "  if (!c) {"
            , "    printf(\"assertion %s failed at time %lld\\n\", assert_names[id], clk);"
            , "  }"
            , "}"
            , ""
            , "// ---- BEGIN of source automatically generated by LIMA ----"
            ]
  , unlines [ "// ---- END of source automatically generated by LIMA ----"
            , ""
            , "int main(int argc, char **argv) {"
            , "  while(1) { " ++ nm ++ "(); usleep(500); }"
            , "}"
            ]
  )

-- | Custom pre-post header for generated C
prePostH :: [Name] -> [Name] -> [(Name, Type)] -> (String, String)
prePostH assertNames _covNames _probeNames =
  ( "void assert(int, bool, int64_t);"
  , ""
  )
